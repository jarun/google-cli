#compdef googler
#
# Completion definition for googler.
#
# Author:
#   Zhiming Wang <zmwangx@gmail.com>
#

setopt localoptions noshwordsplit noksharrays

_googler_query_caching_policy () {
    # rebuild if cache is more than a day old
    local -a oldp
    oldp=( $1(Nm+1) )
    (( $#oldp ))
}

_googler_complete_query () {
    local prefix=$words[CURRENT]
    [[ -n $prefix && $prefix != -* ]] || return

    local cache_id=googler_$prefix
    zstyle -s :completion:${curcontext}: cache-policy update_policy
    [[ -z $update_policy ]] && zstyle :completion:${curcontext}: cache_policy _googler_query_caching_policy

    local -a completions
    if _cache_invalid $cache_id || ! _retrieve_cache $cache_id; then
        completions=( ${(f)"$(googler --complete $prefix 2>/dev/null)"} )
        _store_cache $cache_id completions
    fi

    compadd $@ -- $completions
}

_googler_sites () {
    typeset -a sites
    sites=(
        'amazon.com:Amazon.com'
        'alternativeto.net:AlternativeTo'
        'developer.android.com:Android Developers'
        'infocenter.arm.com:ARM Information Center'
        'asciinema.org:asciinema'
        'askubuntu.com:Ask Ubuntu'
        'bbs.archlinux.org:Arch Forums'
        'aur.archlinux.org:Arch User Repository'
        'wiki.archlinux.org:Arch Wiki'
        'azlyrics.com:AZLyrics'
        'bbc.co.uk:BBC'
        'britannica.com:Encyclopaedia Britannica'
        'crunchbase.com:crunchbase'
        'chrome.google.com:Chrome Extensions'
        'craigslist.org:craigslist'
        'commandlinefu.com:commandlinefu'
        'cnn.com:CNN'
        'cc.com:Comedy Central'
        'en.cppreference.com:CPP Reference'
        'cracked.com:Cracked.com'
        'espncricinfo.com:Cricinfo'
        'thefreedictionary.com:The Free Dictionary'
        'dictionary.com:Dictionary.com'
        'distrowatch.com:DistroWatch'
        'dailynaturalremedies.com:Daily Natural Remedies'
        'packages.debian.org:Debian Package Search'
        'ebay.com:eBay'
        'epguides.com:Episode Guides'
        'embedded.com:Embedded'
        'espn.com:ESPN'
        'etsy.com:Etsy'
        'etymonline.com:Online Etymology Dictionary'
        'facebook.com:Facebook'
        'fandango.com:Fandango Movie Reviews'
        'addons.mozilla.org:Firefox Add-ons'
        'forbes.com:Forbes'
        'forvo.com:Forvo'
        'markets.ft.com:Financial Times'
        'genius.com:Genius Lyrics'
        'github.com:GitHub'
        'gnu.org:GNU'
        'gnupg.org:The GNU Privacy Guard'
        'gutenberg.org:Project Gutenberg'
        'hackaday.com:Hackaday'
        'healthline.com:Healthline'
        'history.com:History'
        'news.ycombinator.com:Hacker News'
        'howstuffworks.com:HowStuffWorks'
        'howtoforge.com:HowtoForge'
        'hulu.com:Hulu'
        'ieee.org:IEEE'
        'ietf.org:IETF'
        'datatracker.ietf.org:IETF Datatracker'
        'instagram.com:Instagram'
        'imdb.com:IMDB'
        'internet-radio.com:Internet Radio'
        'kernel.org:The Linux Kernel Archives'
        'khanacademy.org:Khan Academy'
        'last.fm:Last.fm'
        'linkedin.com:LinkedIn'
        'linux.com:Linux.com'
        'linuxjournal.com:Linux Journal'
        'linuxquestions.org:LinuxQuestions'
        'wiki.linuxquestions.org:LQWiki'
        'lwn.net:LWN.net'
        'lxr.free-electrons.com:Linux Cross Reference'
        'manpages.ubuntu.com:Ubuntu Manpage'
        'man7.org:Linux manual page'
        'mangareader.net:Manga Reader'
        'mlb.mlb.com:MLB'
        'macrumors.com:Mac Rumors'
        'nptel.ac.in:National Programme on Technology Enhanced Learning'
        'ocw.mit.edu:MIT OpenCourseWare'
        'openembedded.org:Open Embedded'
        'opensubtitles.org:OpenSubtitles'
        'opensource.com:Opensource.com'
        'osalt.com:Open Source Alternative'
        'wiki.osdev.org:OSDev Wiki'
        'openwrt.org:OpenWrt'
        'en.oxforddictionaries.com:Oxford Dictionary'
        'patents.google.com:Google Patents'
        'thepiratebay.org:The Pirate Bay'
        'play.google.com:Android Apps'
        'playonlinux.com:PlayOnLinux'
        'docs.python.org:Python documentation'
        'en.wikiquote.org:Wikiquote'
        'reddit.com:Reddit'
        'rd.com:Readers Digest'
        'rfc-editor.org:RFC Reader'
        'rpmfind.net:Rpmfind'
        'rottentomatoes.com:Rotten Tomatoes'
        'onlineslangdictionary.com:OnlineSlangDictionary'
        'stackoverflow.com:Stack Overflow'
        'softpedia.com:Softpedia'
        'sourceforge.net:SurceForge'
        'subscene.com:Subscene'
        'store.steampowered.com:Steam'
        'thesaurus.com:Thesaurus.com'
        'ted.com:TED Talks'
        'tldp.org:The Linux Documentation Project'
        'tldrlegal.com:tl;drLegal'
        'torrentz2.eu:Torrentz2'
        'thepiratebay.org:The Pirate Bay'
        'tunein.com:TuneIn'
        'twitter.com:Twitter'
        'twitch.tv:Twitch'
        'ubuntuforums.org:Ubuntu Forums'
        'packages.ubuntu.com:Ubuntu Packages'
        'wiki.ubuntu.com:Ubuntu Wiki'
        'vim.org:Vim Wiki'
        'en.wikipedia.org:Wikipedia'
        'walmart.com:Walmart'
        'weather.com:Weather.com'
        'wikia.com:Wikia'
        'xkcd.com:XKCD'
        'yahoo.com:Yahoo'
        'finance.yahoo.com:Yahoo Finance'
        'youtube.com:YouTube'
        'zdnet.com:ZDNet'
    )
    _describe 'sites' sites
}

_googler_news () {
    typeset -a news
    news=(
        'nature.com:Nature Research'
        'nba.com:NBA'
        'nationalgeographic.com:National Geographic'
    )
    _describe 'news' news
}

local -a args
args=(
    '(- : *)'{-h,--help}'[show help text and exit]'
    '(-s --start)'{-s,--start}'[start at the Nth result]:result number'
    '(-n --count)'{-n,--count}'[show specified number of results (default 10)]:count'
    '(-N --news)'{-N,--news}'[show results from news section]:news:_googler_news'
    '(-V --videos)'{-V,--videos}'[show results from videos section]'
    '(-c --tld)'{-c,--tld}'[country-specific search with top-level domain]:top level domain without dot'
    '(-l --lang)'{-l,--lang}'[display in specified language]:language code'
    '(-g --geoloc)'{-g,--geoloc}'[specify geolocation]:code'
    '(-x --exact)'{-x,--exact}'[disable automatic spelling correction]'
    '(--colorize)--colorize[whether to colorize output]:auto/always/never'
    '(-C --nocolor)'{-C,--nocolor}'[disable color output]'
    '(--colors)--colors[set output colors]:six-letter string'
    '(-j --first --lucky)'{-j,--first,--lucky}'[open the first result in a web browser]'
    '(-t --time)'{-t,--time}'[time limit search]:period (h/d/w/m/y + number)'
    '(--from)--from[starting date/month/year of date range]:date m/d/yyyy'
    '(--to)--to[ending date/month/year of date range]:date m/d/yyyy'
    '(-w --site)'{-w,--site}'[search a site using Google]:domain:_googler_sites'
    '(-e --exclude)'{-e,--exclude}'[exclude site from results]:domain'
    '(--unfilter)--unfilter[do not omit similar results]'
    '(-p --proxy)'{-p,--proxy}'[proxy in HOST:PORT format]:proxy details'
    '(--notweak)--notweak[disable TCP optimizations, forced TLS 1.2]'
    '(--json)--json[output in JSON format; implies --exact and --noprompt]'
    '(--url-handler)--url-handler[cli script or utility]:url opener'
    '(--show-browser-logs)--show-browser-logs[do not suppress browser output]'
    '(--np --noprompt)'{--np,--noprompt}'[perform search and exit, do not prompt for further interactions]'
    '(-u --upgrade)'{-u,--upgrade}'[perform in-place self-upgrade]'
    '(--include-git)--include-git[when used with --upgrade, upgrade to git master]'
    '(- : *)'{-v,--version}'[show version number and exit]'
    '(-d --debug)'{-d,--debug}'[enable debugging]'
)
_arguments -S -s $args
